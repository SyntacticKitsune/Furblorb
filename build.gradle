plugins {
	id 'java-library'
	id 'eclipse'
	id 'jvm-test-suite'
	id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'net.syntactickitsune'
version = '1.0.0'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
java.withSourcesJar()

sourceSets {
	cli {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

configurations {
	cliImplementation.extendsFrom implementation
	cliCompileOnly.extendsFrom compileOnly
	testCompileOnly.extendsFrom compileOnly
}

testing {
	suites {
		test {
			useJUnitJupiter()
		}
	}
}

repositories {
	mavenCentral()
	maven {
		url 'https://jitpack.io/'
		content {
			includeGroup 'com.github.Querz'
		}
	}
}

dependencies {
	compileOnly 'org.jetbrains:annotations:24.1.0'
	api 'com.google.code.gson:gson:2.10.1'

	testImplementation 'com.github.Querz:NBT:6.1'
	testImplementation 'org.reflections:reflections:0.10.2'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
}

tasks.withType(AbstractArchiveTask).configureEach {
	preserveFileTimestamps = false
	reproducibleFileOrder = true
}

tasks.withType(Jar).configureEach {
	manifest {
		attributes([
			'Automatic-Module-Name': 'net.syntactickitsune.furblorb'
		])
	}
}

tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar).configure {
	archiveClassifier = 'cli'
	from sourceSets.cli.output
	exclude 'module-info.class'
	exclude 'META-INF/maven/**'
	exclude 'META-INF/versions/9/**'

	manifest {
		attributes([
			'Main-Class': 'net.syntactickitsune.furblorb.cli.Furblorb',
			'Automatic-Module-Name': 'net.syntactickitsune.furblorb'
		])
	}
}

sourcesJar {
	from sourceSets.cli.allSource
}

artifacts {
	archives shadowJar
}

processCliResources {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE

	inputs.property 'version', version

	from(sourceSets.cli.resources.srcDirs) {
		include 'version.txt'

		expand 'version':version
	}

	from(sourceSets.cli.resources.srcDirs) {
		exclude 'version.txt'
	}
}